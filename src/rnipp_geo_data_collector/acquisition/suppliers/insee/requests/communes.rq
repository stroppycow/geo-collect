PREFIX igeo: <http://rdf.insee.fr/def/geo#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema>

SELECT ?uri ?insee_code ?label ?article_code ?parent_arrondissement_uri ?parent_departement_uri ?parent_com_uri ?start_date ?end_date ?parent_arrondissement_count ?parent_departement_count ?parent_com_count ?start_date_count ?end_date_count
WHERE {
    ?uri a igeo:Commune .
    ?uri igeo:codeINSEE ?insee_code .
    ?uri igeo:nom ?label .
    OPTIONAL {?uri igeo:codeArticle ?article_code .} .
    OPTIONAL {
        SELECT ?uri (GROUP_CONCAT(DISTINCT ?URISub; SEPARATOR="|") AS ?parent_arrondissement_uri) (COUNT(DISTINCT ?URISub) AS ?parent_arrondissement_count_temp)
        WHERE {
            ?uri igeo:subdivisionDirecteDe ?URISub .
            ?URISub a igeo:Arrondissement 
        } GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (GROUP_CONCAT(DISTINCT ?URISub; SEPARATOR="|") AS ?parent_departement_uri) (COUNT(DISTINCT ?URISub) AS ?parent_departement_count_temp)
        WHERE {
            ?uri igeo:subdivisionDirecteDe ?URISub .
            ?URISub a igeo:Departement 
        } GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (GROUP_CONCAT(DISTINCT ?URISub; SEPARATOR="|") AS ?parent_com_uri) (COUNT(DISTINCT ?URISub) AS ?parent_com_count_temp)
        WHERE {
            ?uri igeo:subdivisionDirecteDe ?URISub .
            ?URISub a igeo:CollectiviteDOutreMer 
        } GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (GROUP_CONCAT(DISTINCT STR(?dateCrea); SEPARATOR="|") AS ?start_date) (COUNT(DISTINCT ?dateCrea) AS ?start_date_count_temp)
        WHERE {
            ?URIEvt a igeo:EvenementGeographique .
            ?URIEvt igeo:creation ?uri .
            ?URIEvt igeo:date ?dateCrea
        }
        GROUP BY ?uri
    }
    OPTIONAL {
        SELECT ?uri (GROUP_CONCAT(DISTINCT STR(?dateSupp); SEPARATOR="|") AS ?end_date) (COUNT(DISTINCT ?dateSupp) AS ?end_date_count_temp)
        WHERE {
            ?URIEvt a igeo:EvenementGeographique .
            ?URIEvt igeo:suppression ?uri .
            ?URIEvt igeo:date ?dateSupp
        }
        GROUP BY ?uri
    }
    BIND(IF(BOUND(?parent_arrondissement_count_temp), ?parent_arrondissement_count_temp, "0"^^xsd:integer ) AS  ?parent_arrondissement_count)
    BIND(IF(BOUND(?parent_departement_count_temp), ?parent_departement_count_temp, "0"^^xsd:integer ) AS  ?parent_departement_count)
    BIND(IF(BOUND(?parent_com_count_temp), ?parent_com_count_temp, "0"^^xsd:integer ) AS  ?parent_com_count)
    BIND(IF(BOUND(?start_date_count_temp), ?start_date_count_temp, "0"^^xsd:integer ) AS  ?start_date_count)
    BIND(IF(BOUND(?end_date_count_temp), ?end_date_count_temp, "0"^^xsd:integer ) AS  ?end_date_count)
}
